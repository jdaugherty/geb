buildscript {
    repositories {
        maven { url = 'https://repo.grails.org/grails/core' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'org.grails.grails-publish'
}

version = projectVersion
group = 'org.grails.plugins'

apply plugin: 'java-library'
apply plugin: 'java-test-fixtures'
apply plugin: 'org.grails.grails-plugin'

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

repositories {
    maven { url = 'https://repo.grails.org/grails/core' }
}

configurations.register('documentation')

ext.set('selenium.version', seleniumVersion) // Override selenium version from spring-boot-dependencies

dependencies {

    implementation(platform("org.grails:grails-bom:$grailsVersion"))

    compileOnly 'org.grails:grails-core' // Provided, as this is a Grails plugin

    testFixturesCompileOnly 'jakarta.servlet:jakarta.servlet-api'
    testFixturesCompileOnly 'org.slf4j:slf4j-simple' // Remove compilation warning about missing slf4j impl
    testFixturesApi 'org.gebish:geb-spock'
    testFixturesApi 'org.grails:grails-testing-support'
    testFixturesApi 'org.grails:grails-datastore-gorm'
    testFixturesApi "org.testcontainers:selenium:$testcontainersVersion"
    testFixturesApi "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testFixturesApi "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"

    documentation 'org.apache.groovy:groovy'
    documentation 'org.apache.groovy:groovy-ant'
    documentation 'org.apache.groovy:groovy-templates'
    documentation 'org.apache.groovy:groovy-xml'
    documentation 'com.github.javaparser:javaparser-core'
}

grailsPublish {
    githubSlug = 'grails/geb'
    license {
        name = 'Apache-2.0'
    }
    title = 'Grails Geb Plugin'
    desc = 'Provides Integration with Geb for Functional Testing'
    developers = [
        graemerocher: 'Graeme Rocher',
        puneetbehl: 'Puneet Behl',
        sbglasius: 'SÃ¸ren Berg Glasius',
        matrei: 'Mattias Reichel',
        jdaugherty: 'James Daugherty',
    ]
}

tasks.withType(Groovydoc).configureEach {
    destinationDir = layout.buildDirectory.dir('docs/api').get().asFile
    docTitle = "Grails Geb Plugin ${version}"
    classpath = configurations.documentation
    source = files(source, sourceSets.testFixtures.allSource)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
    }
}

tasks.named('bootJar') { enabled = false }
tasks.named('bootRun') { enabled = false }
tasks.named('bootTestRun') { enabled = false }
tasks.named('findMainClass') { enabled = false }